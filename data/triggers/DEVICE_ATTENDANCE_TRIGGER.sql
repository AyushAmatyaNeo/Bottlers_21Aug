create or replace TRIGGER DEVICE_ATTENDANCE_TRIGGER AFTER
  INSERT ON HRIS_ATTENDANCE FOR EACH ROW DECLARE V_IN_TIME HRIS_ATTENDANCE_DETAIL.IN_TIME%TYPE;
  V_SHIFT_ID HRIS_SHIFTS.SHIFT_ID%TYPE;
  V_OVERALL_STATUS HRIS_ATTENDANCE_DETAIL.OVERALL_STATUS%TYPE;
  V_LATE_STATUS HRIS_ATTENDANCE_DETAIL.LATE_STATUS%TYPE:='N';
  V_START_TIME HRIS_SHIFTS.START_TIME%TYPE ;
  V_END_TIME HRIS_SHIFTS.END_TIME%TYPE ;
  V_GRACE_START_TIME HRIS_SHIFTS.GRACE_START_TIME%TYPE;
  V_GRACE_END_TIME HRIS_SHIFTS.GRACE_END_TIME%TYPE ;
  V_LATE_IN HRIS_SHIFTS.LATE_IN%TYPE;
  V_EARLY_OUT HRIS_SHIFTS.EARLY_OUT%TYPE;
  V_LATE_START_TIME TIMESTAMP;
  V_EARLY_END_TIME  TIMESTAMP;
  V_ADJUSTED_START_TIME HRIS_SHIFT_ADJUSTMENT.START_TIME%TYPE:=NULL;
  V_ADJUSTED_END_TIME HRIS_SHIFT_ADJUSTMENT.END_TIME%TYPE    :=NULL;
  V_LATE_COUNT NUMBER                                        :=0;
  BEGIN
    --
    BEGIN
      SELECT IN_TIME,
        SHIFT_ID,
        OVERALL_STATUS,
        LATE_STATUS
      INTO V_IN_TIME,
        V_SHIFT_ID,
        V_OVERALL_STATUS,
        V_LATE_STATUS
      FROM HRIS_ATTENDANCE_DETAIL
      WHERE ATTENDANCE_DT = TO_DATE (:new.ATTENDANCE_DT, 'DD-MON-YY')
      AND EMPLOYEE_ID     = :new.EMPLOYEE_ID;
    EXCEPTION
    WHEN NO_DATA_FOUND THEN
      DBMS_OUTPUT.PUT_LINE ('Attendance Job for '||:new.ATTENDANCE_DT||' not excecuted');
      RETURN;
    END;
    --
    BEGIN
      SELECT S.START_TIME ,
        S.END_TIME ,
        S.GRACE_START_TIME,
        S.GRACE_END_TIME ,
        S.LATE_IN,
        S.EARLY_OUT,
        S.START_TIME+(.000694*NVL(S.LATE_IN,0)),
        S.END_TIME  -(.000694*NVL(S.EARLY_OUT,0))
      INTO V_START_TIME,
        V_END_TIME,
        V_GRACE_START_TIME,
        V_GRACE_END_TIME,
        V_LATE_IN,
        V_EARLY_OUT,
        V_LATE_START_TIME,
        V_EARLY_END_TIME
      FROM HRIS_SHIFTS S
      WHERE S.SHIFT_ID=V_SHIFT_ID ;
    EXCEPTION
    WHEN NO_DATA_FOUND THEN
      RAISE_APPLICATION_ERROR(-20344, 'SHIFT WITH SHIFT_ID => '|| V_SHIFT_ID ||' NOT FOUND.');
    END;
    --   CHECK FOR ADJUSTED SHIFT
    BEGIN
      SELECT SA.START_TIME,
        SA.END_TIME
      INTO V_ADJUSTED_START_TIME,
        V_ADJUSTED_END_TIME
      FROM HRIS_SHIFT_ADJUSTMENT SA
      JOIN HRIS_EMPLOYEE_SHIFT_ADJUSTMENT ESA
      ON (SA.ADJUSTMENT_ID=ESA.ADJUSTMENT_ID)
      WHERE (TRUNC(:new.ATTENDANCE_DT) BETWEEN TRUNC(SA.ADJUSTMENT_START_DATE) AND TRUNC(SA.ADJUSTMENT_END_DATE) )
      AND ESA.EMPLOYEE_ID =:new.EMPLOYEE_ID;
    EXCEPTION
    WHEN NO_DATA_FOUND THEN
      DBMS_OUTPUT.PUT_LINE('NO ADJUSTMENT FOUND FOR EMPLOYEE =>'|| :new.EMPLOYEE_ID || 'ON THE DATE'||:new.ATTENDANCE_DT);
    END;
    IF(V_ADJUSTED_START_TIME IS NOT NULL) THEN
      V_START_TIME           :=V_ADJUSTED_START_TIME;
      V_LATE_START_TIME      := V_START_TIME+(.000694*NVL(V_LATE_IN,0));
    END IF;
    IF(V_ADJUSTED_END_TIME IS NOT NULL) THEN
      V_END_TIME           :=V_ADJUSTED_END_TIME;
      V_EARLY_END_TIME     := V_END_TIME -(.000694*NVL(V_EARLY_OUT,0));
    END IF;
    --      END FOR CHECK FOR ADJUSTED_SHIFT
    IF (V_IN_TIME            IS NULL) THEN
      IF(V_OVERALL_STATUS     ='DO') THEN
        V_OVERALL_STATUS     :='WD';
      ELSIF (V_OVERALL_STATUS ='HD') THEN
        V_OVERALL_STATUS     :='WH';
      ELSIF (V_OVERALL_STATUS ='LV') THEN
        V_OVERALL_STATUS     :='LP';
      ELSIF (V_OVERALL_STATUS ='TV') THEN
        V_OVERALL_STATUS     :='VP';
      ELSIF (V_OVERALL_STATUS ='TN') THEN
        V_OVERALL_STATUS     :='TP';
      ELSE
        V_OVERALL_STATUS:='PR';
      END IF;
      --
      IF (V_LATE_START_TIME-TRUNC(V_LATE_START_TIME))<(:new.ATTENDANCE_TIME-TRUNC(:new.ATTENDANCE_TIME)) THEN
        V_LATE_STATUS                               :='L';
      END IF;
      --
      UPDATE HRIS_ATTENDANCE_DETAIL
      SET IN_TIME         = TO_DATE ( TO_CHAR (:new.ATTENDANCE_TIME, 'DD-MON-YY HH:MI AM'), 'DD-MON-YY HH:MI AM'),
        OVERALL_STATUS    = V_OVERALL_STATUS,
        LATE_STATUS       = V_LATE_STATUS,
        IN_REMARKS        = :new.REMARKS
      WHERE ATTENDANCE_DT = TO_DATE (:new.ATTENDANCE_DT, 'DD-MON-YY')
      AND EMPLOYEE_ID     = :new.EMPLOYEE_ID;
      RETURN;
    END IF;
    --
    IF (V_EARLY_END_TIME-TRUNC(V_EARLY_END_TIME))>(:new.ATTENDANCE_TIME-TRUNC(:new.ATTENDANCE_TIME)) THEN
      IF (V_LATE_STATUS                          = 'L') THEN
        V_LATE_STATUS                           :='B';
      ELSE
        V_LATE_STATUS :='E';
      END IF;
    ELSE
      IF (V_LATE_STATUS     ='B') THEN
        V_LATE_STATUS      :='L';
      ELSIF ( V_LATE_STATUS ='E') THEN
        V_LATE_STATUS      := 'N';
      END IF;
    END IF;
    UPDATE HRIS_ATTENDANCE_DETAIL
    SET OUT_TIME        = TO_DATE ( TO_CHAR (:new.ATTENDANCE_TIME, 'DD-MON-YY HH:MI AM'), 'DD-MON-YY HH:MI AM'),
      LATE_STATUS       =V_LATE_STATUS,
      OUT_REMARKS       = :new.REMARKS
    WHERE ATTENDANCE_DT = TO_DATE (:new.ATTENDANCE_DT, 'DD-MON-YY')
    AND EMPLOYEE_ID     = :new.EMPLOYEE_ID;
  END;