create or replace PROCEDURE HRIS_EMPLOYEE_SETUP_PROC(
    P_EMPLOYEE_ID HRIS_EMPLOYEES.EMPLOYEE_ID%TYPE)
AS
  V_JOIN_DATE HRIS_EMPLOYEES.JOIN_DATE%TYPE;
  V_FISCAL_YEAR_ID HRIS_FISCAL_YEARS.FISCAL_YEAR_ID%TYPE;
  V_MONTH_ID HRIS_MONTH_CODE.MONTH_ID%TYPE;
  V_CURRENT_MONTH_COUNT NUMBER;
  V_DEFAULT_DAYS        NUMBER;
  V_PRODATA_DAYS        NUMBER;
BEGIN
  BEGIN
    SELECT JOIN_DATE
    INTO V_JOIN_DATE
    FROM HRIS_EMPLOYEES
    WHERE EMPLOYEE_ID = P_EMPLOYEE_ID;
  EXCEPTION
  WHEN NO_DATA_FOUND THEN
    SYS.DBMS_OUTPUT.PUT_LINE('NO JOIN_DATE SET FROM THE EMPLOYEE WITH EMPLOYEE_ID : '||P_EMPLOYEE_ID);
    RETURN;
  END;
  BEGIN
    SELECT FISCAL_YEAR_ID,
      MONTH_ID,
      MONTH_NO
    INTO V_FISCAL_YEAR_ID,
      V_MONTH_ID,
      V_CURRENT_MONTH_COUNT
    FROM HRIS_MONTH_CODE
    WHERE TRUNC(V_JOIN_DATE) BETWEEN FROM_DATE AND TO_DATE;
  EXCEPTION
  WHEN no_data_found THEN
    SYS.DBMS_OUTPUT.PUT('No Current Month found.');
    RETURN;
  END;
  BEGIN
    FOR leave IN
    (SELECT LEAVE_ID,
      DEFAULT_DAYS,
      IS_PRODATA_BASIS
    FROM HRIS_LEAVE_MASTER_SETUP
    WHERE STATUS                 ='E'
    AND ASSIGN_ON_EMPLOYEE_SETUP ='Y'
    )
    LOOP
      V_DEFAULT_DAYS           := leave.DEFAULT_DAYS;
      V_PRODATA_DAYS           := leave.DEFAULT_DAYS;
      IF leave.IS_PRODATA_BASIS = 'Y' THEN
        V_PRODATA_DAYS         :=ROUND(V_DEFAULT_DAYS*((13-V_CURRENT_MONTH_COUNT)/12));
      END IF;
      INSERT
      INTO HRIS_EMPLOYEE_LEAVE_ASSIGN
        (
          EMPLOYEE_ID,
          LEAVE_ID,
          PREVIOUS_YEAR_BAL,
          TOTAL_DAYS,
          BALANCE,
          FISCAL_YEAR,
          CREATED_DT
        )
        VALUES
        (
          P_EMPLOYEE_ID,
          leave.LEAVE_ID,
          0,
          V_PRODATA_DAYS,
          V_PRODATA_DAYS,
          V_FISCAL_YEAR_ID,
          TRUNC(SYSDATE)
        );
    END LOOP;
  END;
  BEGIN
    FOR holiday IN
    (SELECT HOLIDAY_ID
      FROM HRIS_HOLIDAY_MASTER_SETUP
      WHERE ASSIGN_ON_EMPLOYEE_SETUP = 'Y'
      AND STATUS                     ='E'
      AND START_DATE                >=TRUNC(V_JOIN_DATE)
    )
    LOOP
      INSERT
      INTO HRIS_EMPLOYEE_HOLIDAY
        (
          EMPLOYEE_ID,
          HOLIDAY_ID
        )
        VALUES
        (
          P_EMPLOYEE_ID,
          holiday.HOLIDAY_ID
        );
    END LOOP;
  END;
END;